apply plugin: 'com.android.application'

project.ext.set("versionMajor", "1")
project.ext.set("versionMinor", "2")
project.ext.set("versionRevision", "0")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.nateshoffner.seachemdoser"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 3
        versionName buildVersionName()
        resConfig "en"
        resValue "string", "build_revision", revision()
        resValue "string", "version_name", versionName
        resValue "string", "version_name_human", buildHumanVersionName()
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

ext {
    supportLibVersion = '25.0.0'
    versionName = '1.2.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:preference-v14:${supportLibVersion}"
    compile "uk.co.chrisjenx:calligraphy:2.2.0"
    compile('com.mikepenz:materialdrawer:5.7.0@aar') {
        transitive = true
    }
    compile 'com.mikepenz:google-material-typeface:2.2.0.1.original@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.5.0.1@aar'
    compile('com.afollestad.material-dialogs:core:0.9.1.0@aar') {
        transitive = true
    }
    compile 'com.afollestad.material-dialogs:commons:0.9.1.0'
    compile('com.mikepenz:aboutlibraries:5.8.5@aar') {
        transitive = true
    }
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString()
}

def buildNumber() {
    return System.getenv('TRAVIS_BUILD_NUMBER') ?: "0"
}

def buildBaseVersion() {
    def major = project.ext.versionMajor
    def minor = project.ext.versionMinor
    def revision = project.ext.versionRevision

    def version = major + "." + minor

    if (revision != "0")
        version = version + "." + revision

    return version
}

def buildVersionName(){
    def version = buildBaseVersion()
    def buildNumber = buildNumber()

    if (buildNumber != "0")
        version = version + "." + buildNumber

    return version
}

def buildHumanVersionName(){
    def version = buildBaseVersion()
    def buildNumber = buildNumber()

    if (buildNumber != "0")
        version = version + " (Build " + buildNumber + ")"

    return version
}